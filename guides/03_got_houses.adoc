= Game of Thrones: Houses
:csv-url: https://raw.githubusercontent.com/mneedham/neo4j-got/master/data/import/
:icons: font

== Importing houses

In this next section we're going to import the houses that characters belong to.

Run the following query to explore the houses CSV file:

[source, cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}houses.csv" AS row
RETURN row
----

Now let's create a node with the `House` label for each row in the file:

[source, cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}houses.csv" AS row
MERGE (house:House {id: row.link})
ON CREATE SET house.name = row.name
----

Run the following query to return all the houses:

[source, cypher]
----
MATCH (house:House)
RETURN house
----

You should see 73 nodes if the import has worked as expected.

== Exercise: Create allegiances

Now it's your turn!
Run the following query to view the allegiances between characters and houses:

[source, cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}characters_houses.csv" AS row
RETURN row.character, row.house
----

Now create a `HAS_ALLEGIANCE_TO` relationship for each character/house pair in the file.

== Answer: Create allegiances

[source, cypher,subs=attributes]
----
LOAD CSV WITH HEADERS FROM "{csv-url}characters_houses.csv" AS row
MATCH (character:Character {id: row.character})
MATCH (house:House {id: row.house})
MERGE (character)-[:HAS_ALLEGIANCE_TO]->(house)
----

== Exploring allegiances

Run the following query to check that the allegiances have been created:

[source, cypher]
----
MATCH (character:Character)-[:HAS_ALLEGIANCE_TO]->(house)
RETURN character.id, character.name, COUNT(*) AS allegiances
ORDER BY allegiances DESC
----

You should see `Randyll Tarly` in first place with 4 houses.

= Exercise: What houses do people have allegiance to?

Time for another mini exercise.

See if you can tweak the query from the previous slide to include the names of the houses as well as the count.

_Tip_ The link:https://neo4j.com/docs/developer-manual/current/#_collecting_aggregation[`COLLECT` function] will come in helpful.

= Answer: What houses do people have allegiance to?

We just need to add a call to `COLLECT` as part of our `RETURN` statement:

[source, cypher]
----
MATCH (character:Character)-[:HAS_ALLEGIANCE_TO]->(house)
RETURN character.id, character.name, COLLECT(house.name) AS houses, COUNT(*) AS allegiances
ORDER BY allegiances DESC
----



== Combining queries together

Let's revisit one of our queries from the first section where we found the characters who'd appeared in the most episodes:

[source, cypher]
----
MATCH (character:Character)-[:APPEARED_IN]->()
RETURN character.name, COUNT(*) AS appearances
ORDER BY appearances DESC
----

[source, cypher]
----
MATCH (house:House)<-[:HAS_ALLEGIANCE_TO]-(character:Character)
WITH character, COLLECT(house.name) AS houses
MATCH (character)-[:APPEARED_IN]->()
RETURN character.name, houses, COUNT(*) AS appearances
ORDER BY appearances DESC
----


[source, cypher]
----
MATCH (house:House)
RETURN house
----
